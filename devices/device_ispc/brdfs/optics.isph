// ======================================================================== //
// Copyright 2009-2013 Intel Corporation                                    //
//                                                                          //
// Licensed under the Apache License, Version 2.0 (the "License");          //
// you may not use this file except in compliance with the License.         //
// You may obtain a copy of the License at                                  //
//                                                                          //
//     http://www.apache.org/licenses/LICENSE-2.0                           //
//                                                                          //
// Unless required by applicable law or agreed to in writing, software      //
// distributed under the License is distributed on an "AS IS" BASIS,        //
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. //
// See the License for the specific language governing permissions and      //
// limitations under the License.                                           //
// ======================================================================== //

#pragma once

#include "../samplers/sample.isph"
#include "../samplers/shapesampler.isph"

/*! \addtogroup rivl_render_embree_ivl */
/*! @{ */

/*! Reflects a viewing vector V at a normal N. */
inline Sample3f reflect(const vec3f &V, const vec3f &N) {
  float cosi = dot(V,N);
  return make_Sample3f(sub(mul(2.0f*cosi,N),V), 1.0f);
}

/*! Reflects a viewing vector V at a normal N. Cosine between V
 *  and N is given as input. */
inline Sample3f reflect(const vec3f &V, const vec3f &N, const float cosi) {
  return make_Sample3f(sub(mul(2.0f*cosi,N),V), 1.0f);
}

// =======================================================
/*!Refracts a viewing vector V at a normal N using the relative
 *  refraction index eta. Eta is refraction index of outside medium
 *  (where N points into) divided by refraction index of the inside
 *  medium. The vectors V and N have to point towards the same side
 *  of the surface. The cosine between V and N is given as input and
 *  the cosine of -N and transmission ray is computed as output. */
inline Sample3f refract(const vec3f V, const vec3f N, const float eta, 
                        const float cosi, float &cost)
{
  const float k = 1.0f-eta*eta*(1.0f-cosi*cosi);
  if (k < 0.0f) { cost = 0.0f; return make_Sample3f(make_vec3f(0.f),0.0f); }
  cost = sqrt(k);
  return make_Sample3f(sub(mul(eta,sub(mul(cosi,N),V)),mul(cost,N)), sqr(eta));
}

/*! Computes fresnel coefficient for media interface with relative
 *  refraction index eta. Eta is the outside refraction index
 *  divided by the inside refraction index. Both cosines have to be
 *  positive. */
inline float fresnelDielectric(const float cosi, const float cost, const float eta)
{
  const float Rper = (eta*cosi -     cost) * rcp(eta*cosi +     cost);
  const float Rpar = (    cosi - eta*cost) * rcp(    cosi + eta*cost);
  return 0.5f*(Rpar*Rpar + Rper*Rper);
}

  /*! Computes fresnel coefficient for media interface with relative
   *  refraction index eta. Eta is the outside refraction index
   *  divided by the inside refraction index. The cosine has to be
   *  positive. */
inline float fresnelDielectric(const float cosi, const float eta)
{
  const float k = 1.0f-eta*eta*(1.0f-cosi*cosi);
  if (k < 0.0f) return 1.0f;
  const float cost = sqrt(k);
  return fresnelDielectric(cosi, cost, eta);
}

/*! Computes fresnel coefficient for conductor medium with complex
 *  refraction index (eta,k). The cosine has to be positive. */
inline vec3f fresnelConductor(const float cosi, const vec3f eta, const vec3f k)
{
  const vec3f tmp = add(mul(eta,eta),mul(k,k));
  const vec3f Rpar = mul(  add(sub(mul(tmp,cosi*cosi), mul(2.0f*cosi,eta)), make_vec3f(1.f)),
                         rcp(add(mul(tmp,cosi*cosi), mul(2.0f*cosi,eta) , make_vec3f(1.f))));
  const vec3f Rper = mul(  add(sub(tmp, mul(2.0f*cosi,eta)), make_vec3f(cosi*cosi)), 
                         rcp(add(tmp, mul(2.0f*cosi,eta) , make_vec3f(cosi*cosi))));
  return mul(0.5f,add(Rpar,Rper));
}

// =======================================================
struct FresnelConductor {
  vec3f eta;  //!< Real part of refraction index
  vec3f k;    //!< Imaginary part of refraction index
};

inline vec3f eval(uniform const FresnelConductor &THIS, const float cosTheta) {
  return fresnelConductor(cosTheta,THIS.eta,THIS.k);
}

inline uniform FresnelConductor make_FresnelConductor(const uniform vec3f eta, const uniform vec3f k) {
  uniform FresnelConductor m; m.eta = eta; m.k = k; return m;
}

// =======================================================
struct FresnelDielectric 
{
  /*! refraction index of the medium the incident ray travels in */
  float etai;
  
  /*! refraction index of the medium the outgoing transmission rays
   *  travels in */
  float etat;
};

inline vec3f eval(const uniform FresnelDielectric &THIS, const float cosTheta) {
  return make_vec3f(fresnelDielectric(cosTheta,THIS.etai/THIS.etat));
}

inline uniform FresnelDielectric make_FresnelDielectric(const uniform float etai, const uniform float etat) {
  uniform FresnelDielectric m; m.etai = etai; m.etat = etat; return m;
}

// =======================================================
struct PowerCosineDistribution {
  float exp;
};

inline float eval(const uniform PowerCosineDistribution &THIS, const float cosThetaH) {
  return (THIS.exp+2) * (1.0f/(2.0f*(M_PI))) * pow(abs(cosThetaH), THIS.exp);
}

/*! Samples the power cosine distribution. */
inline void sample(const uniform PowerCosineDistribution &THIS, const vec3f wo, const DifferentialGeometry dg, Sample3f &wi, const vec2f s)  
{
  Sample3f wh = powerCosineSampleHemisphere(s.x,s.y,dg.Ns,THIS.exp);
  Sample3f r = reflect(wo,wh.v);
  wi = make_Sample3f(r.v,wh.pdf/(4.0f*abs(dot(wo,wh.v))));
}

inline uniform PowerCosineDistribution make_PowerCosineDistribution(const uniform float _exp) { 
  uniform PowerCosineDistribution m; m.exp = _exp; return m;
}

/*! @} */
